

allprojects {
    repositories {
        maven { url 'https://jitpack.io' }
        jcenter()
    }
    group 'no.unit.nva'
    version '1.0-SNAPSHOT'

    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'


    project.ext{
        jacksonVersion = '2.10.2'
        awsSdkVersion = '1.11.714'
        junit5Version = '5.6.0'
        nvaCommonsVersion = '0.5.2'
        nvaDatamodelJavaVersion = '0.10.6'
    }



    dependencies {
        def junit5Version = '5.6.0'
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit5Version
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-migrationsupport', version: junit5Version
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit5Version
    }
    test {
        useJUnitPlatform()
        failFast = true
        testLogging {
            events = ['skipped', 'passed', 'failed']
        }
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    pmd {
        ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
        ruleSets = []
        ignoreFailures = false
    }

    checkstyle {
        configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
        showViolations = true
    }

    tasks.withType(Checkstyle) {
        reports {
            xml.enabled false
            html.enabled true
            html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
        }
    }

    check.dependsOn jacocoTestCoverageVerification
    jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                excludes = ['**datacite**']
                limit {
                    counter = 'METHOD'
                    value = 'COVEREDRATIO'
                    minimum = 0.00
                }
            }

            rule {
                limit {
                    counter = 'CLASS'
                    value = 'COVEREDRATIO'
                    minimum = 0.00
                }
                excludes = ['**datacite**']
            }
        }
    }

}